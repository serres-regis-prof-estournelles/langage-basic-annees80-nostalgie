10 CLS
20 PRINT "    _____ê"
30 PRINT "  __|___|__ "
40 PRINT " /         \"
50 PRINT "|    LOCK   |"
60 PRINT "|    0000   |"
70 PRINT " \_________/"
80 PRINT ""
90 PRINT "BIENVENUE DANS LE JEU DU CADENAS !"
100 PRINT "TROUVERAS-TU LA BONNE COMBINAISON ?"
110 PRINT "MONSIEUR SERRES PENSE QUE NON"
120 PRINT "TU DOIS ENTRER 4 CHIFFRES DIFFERENTS"
130 PRINT "***********************************"
140 REM INITIALISATION
150 E=10: REM ESSAIS RESTANTS
160 REM GENERATION COMBINAISON
170 FOR J=1 TO 4
180 A=INT(RND(1)*10)
190 FOR K=1 TO J-1
200 IF C(K)=A THEN 180
210 NEXT K
220 C(J)=A
230 NEXT J
240 REM AFFICHAGE INDICES
250 PRINT "INDICES EN DEBUT DE PARTIE :"
260 REM 2 CHIFFRES DANS LA COMBINAISON
270 A=INT(RND(1)*4)+1
280 B=INT(RND(1)*4)+1
290 IF A=B THEN 280
300 I(1)=C(A)
310 I(2)=C(B)
320 REM 2 CHIFFRES HORS COMBINAISON
330 FOR J=3 TO 4
340 A=INT(RND(1)*10)
350 FOR K=1 TO 4
360 IF C(K)=A THEN 340
370 NEXT K
380 FOR K=1 TO J-1
390 IF I(K)=A THEN 340
400 NEXT K
410 I(J)=A
420 NEXT J
430 REM MELANGE INDICES
440 FOR J=1 TO 4
450 A=INT(RND(1)*4)+1
460 B=INT(RND(1)*4)+1
470 T=I(A): I(A)=I(B): I(B)=T
480 NEXT J
490 REM AFFICHAGE INDICES
500 FOR J=1 TO 4
510 PRINT "*** INDICE ";J;" : LE CHIFFRE ";I(J);
520 F=0
530 FOR K=1 TO 4
540 IF I(J)=C(K) THEN F=1
550 NEXT K
560 IF F=1 THEN PRINT " EST DANS LA COMBINAISON *********"
570 IF F=0 THEN PRINT " N'EST PAS DANS LA COMBINAISON ***"
580 NEXT J
590 PRINT "***********************************"
600 REM BOUCLE PRINCIPALE
610 IF E=0 THEN 930
620 PRINT ""
630 PRINT "IL TE RESTE ";E;" ESSAI(S)"
635 PRINT ""
640 INPUT "ENTRE 4 CHIFFRES DIFFERENTS SANS ESPACES : "; G$
650 REM VERIFICATION SAISIE
660 IF LEN(G$)<>4 THEN PRINT "4 CHIFFRES S'IL TE PLAIT !": GOTO 640
670 REM CONVERSION ET VERIFICATION
680 FOR J=1 TO 4
690 C$=MID$(G$,J,1)
700 IF C$<"0" OR C$>"9" THEN PRINT "CHIFFRES DE 0 A 9 SEULEMENT !": GOTO 640
710 G(J)=VAL(C$)
720 NEXT J
730 REM VERIFICATION CHIFFRES DIFFERENTS
740 FOR J=1 TO 3
750 FOR K=J+1 TO 4
760 IF G(J)=G(K) THEN PRINT "LES CHIFFRES DOIVENT ETRE DIFFERENTS !": GOTO 640
770 NEXT K
780 NEXT J
790 REM EVALUATION
800 BP=0: REM BIEN PLACES
810 MP=0: REM MAL PLACES
820 FOR J=1 TO 4
830 IF G(J)=C(J) THEN BP=BP+1: GOTO 870
840 FOR K=1 TO 4
850 IF G(J)=C(K) THEN MP=MP+1
860 NEXT K
870 NEXT J
880 PRINT "==> CHIFFRES BIEN PLACES : ";BP
890 PRINT "==> CHIFFRES MAL PLACES  : ";MP
895 PRINT ""
900 E=E-1
910 IF BP=4 THEN 980
920 GOTO 610
930 REM PERDU
940 PRINT "***********************************"
950 PRINT "PERDU ! LA COMBINAISON ETAIT : ";
960 FOR J=1 TO 4: PRINT C(J);: NEXT J
970 PRINT "": PRINT "***********************************": END
980 REM GAGNE
990 PRINT "***********************************"
1000 PRINT "BRAVO ! TU AS TROUVE EN ";11-E;" ESSAIS !"
1010 PRINT "LA COMBINAISON ETAIT : ";
1020 FOR J=1 TO 4: PRINT C(J);: NEXT J
1030 PRINT "": PRINT "***********************************"
1040 END
